ACTOR FlameTrails
{
	Game Doom
	Radius 1
	Height 1
	Speed 3
	Damage 0 
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    +NOINTERACTION
	+NOCLIP
	RenderStyle Add
    DamageType Flames
    Scale 0.5
	Alpha 1
	Gravity 0
	States
	{
    Spawn:
        TNT1 A 2
        FRPR ABCDEFGH 3 BRIGHT
        Stop
	}
}

ACTOR GreenFlameTrails: FlameTrails
{
	States
	{
    Spawn:
	    TNT1 A 2
        FRPG CDEFGH 1 BRIGHT
        Stop
	}
}

ACTOR GreenFlameTrailsTeleport: FlameTrails
{
	Alpha 0.2
	States
	{
    Spawn:
	    TNT1 A 2
        FRPG ABCDEFGH 2 BRIGHT
        Stop
	}
}

ACTOR SmallFlameTrails: FlameTrails
{

    Scale 0.3
	States
	{
    Spawn:
        TNT1 A 2
        FRPR ABCDEFGH 3 BRIGHT
        Stop
		}
}

ACTOR SmallGreenFlameTrails: GreenFlameTrails
{

    Scale 0.3
}

ACTOR FireBallExplosion: FlameTrails
{
Scale 1.6
	States
	{
    Spawn:
        FX98 ABCDEFGHIJKL 1 BRIGHT A_SpawnItem("YellowFlare",0,0)
        FX98 MNOPQ 1
        Stop
	}
}

ACTOR ExplosionFlames: FlameTrails
{
Scale 2.2
Speed 2
	States
	{
    Spawn:
        EXPL AA 3 BRIGHT A_SpawnItem("RedFlare",0,0)
		TNT1 AAA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
        EXPL GHII 3 BRIGHT
        Stop
	}
}

ACTOR SmallExplosionFlames: ExplosionFlames
{
Scale 0.9
Speed 3
	States
	{
    Spawn:
        EXPL AA 2 BRIGHT A_SpawnItem("RedFlare",0,0)
        EXPL BCDF 1 BRIGHT //A_SpawnItem("YellowFlare",0,0)
		
        EXPL GHII 1 BRIGHT

        Stop
	}
}

ACTOR MediumExplosionFlames: ExplosionFlames
{
Scale 1.3
Speed 4
	States
	{
    Spawn:
        EXPL AA 2 BRIGHT A_SpawnItem("RedFlare",0,0)
		TNT1 A 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
        EXPL BCDF 1 BRIGHT //A_SpawnItem("YellowFlare",0,0)
        EXPL GHII 1 BRIGHT

        Stop
	}
}

ACTOR FlameBall: FlameTrails
{
	+NOCLIP
    Scale 1.3
	States
	{
    Spawn:
        FRFX AB 1 BRIGHT
        Stop
	}
}

ACTOR FlameBallBarrel: FlameBall
{
	Speed 0
	+NOCLIP
	-FORCEXYBILLBOARD
    Scale 2.3
	States
	{
    Spawn:
        FRFX AB 3 BRIGHT
        Stop
	}
}



ACTOR FlameBallBlue: FlameBall
{
Speed 0
-FORCEXYBILLBOARD
Alpha 1.0
Renderstyle Add
    Scale 1.6
	States
	{
    Spawn:
        FRPB JK 3 BRIGHT
        Stop
	}
}


ACTOR FlameBallGreen: FlameBall
{
Speed 0
-FORCEXYBILLBOARD
Alpha 1.0
Renderstyle Add
    Scale 1.6
	States
	{
    Spawn:
        FRPG JK 3 BRIGHT
        Stop
	}
}

ACTOR FlameBallRed: FlameBall
{
Speed 0
-FORCEXYBILLBOARD
Alpha 1.0
Renderstyle Add
    Scale 1.6
	States
	{
    Spawn:
        FRFX AB 3 BRIGHT
        Stop
	}
}

ACTOR TorchFlameTrails : FlameTrails
{
	Speed 3
    Scale 0.5
	States
	{
    Spawn:
	    TNT1 A 0
        FIR1 AABBCCDEFGHII 1 BRIGHT
        Stop
	}
}

ACTOR TorchFlameTrailsGreen : TorchFlameTrails
{
	States
	{
    Spawn:
	    TNT1 A 0
        FIRG AABBCCDEFGHII 1 BRIGHT
        Stop
	}
}

ACTOR RealFlameTrailsBlue: TorchFlameTrails
{
	States
	{
    Spawn:
	    TNT1 A 0
        FIRB AABBCCDEFGHII 1 BRIGHT
        Stop
	}
}

ACTOR RealFlameTrailsSmallLong: TorchFlameTrails
{
    Scale 1.1
	States
	{
    Spawn:
	    TNT1 A 0
		FIR1 ABC 2 BRIGHT
        FIR2 ABCDE 2 BRIGHT
        Stop
	}
}

ACTOR CoolandNewFlameTrails: FlameTrails
{
    Scale 0.3
	Speed 1
	States
	{
    Spawn:
	    TNT1 A 2
        FIR3 JMP 1 BRIGHT
        Stop
	}
}

ACTOR CoolandNewFlameTrailsLong: FlameTrails
{
    Scale 0.6
	Speed 1
	States
	{
    Spawn:
	    //TNT1 A 2
        FIR3 BCDEFGHIJKLMNOP 1 BRIGHT
        Stop
	}
}


ACTOR FireballExplosionFlamesSmall: FlameTrails
{
    Scale 0.5
	Speed 1
	States
	{
    Spawn:
        FIR3 KLMNOP 1 BRIGHT
        Stop
	}
}

ACTOR RevenantProjectileTrails: FlameTrails
{

    Scale 0.3
	States
	{
    Spawn:
        TNT1 A 2
        FRPR BCDEFGH 1 BRIGHT
        Stop
		}
}

ACTOR MinimalFlameTrails : FlameTrails
{
	Speed 4
    +NOINTERACTION
	+NOCLIP
	+FORCEXYBILLBOARD
    DamageType Flames
    Scale 0.4
	States
	{
    Spawn:
	    TNT1 A 0
        FIR1 FGGHHII 1 BRIGHT
	    Stop
	}
}

ACTOR SmallFireSpawner: SmokeColumn
{
States
	{
		Spawn:
		TNT1 A 1
		TNT1 A 0 A_Jump(128, 4)
		TNT1 A 0 A_SpawnItemEx("SmokeColumn",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("SmallFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAA 0
		Stop
	}
}

ACTOR SmallFire: SmokeColumn
{
	var int user_loop_index;

	Alpha 0.9
	RenderStyle Translucent
	Scale 0.6
	States
	{
	Spawn:
		FLME A 1 BRIGHT A_CustomMissile ("MinimalFlameTrails", 24, 0, random (0, 360), 2, random (70, 110))
		FLME BCD 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 15)
		FLME E 1 BRIGHT A_CustomMissile ("MinimalFlameTrails", 24, 0, random (0, 360), 2, random (70, 110))
		FLME FGH 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 15)
		FLME I 1 BRIGHT A_CustomMissile ("MinimalFlameTrails", 24, 0, random (0, 360), 2, random (60, 130))
		FLME JKL 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 15)
		FLME M 1 BRIGHT A_CustomMissile ("MinimalFlameTrails", 24, 0, random (0, 360), 2, random (60, 130))
		FLME N 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 15)
		TNT1 A 0 A_SetUserVar("user_loop_index", user_loop_index+1)
		TNT1 A 0 A_JumpIf(user_loop_index < 14, "Spawn")
		TNT1 AAAAA 3 BRIGHT A_CustomMissile ("MinimalFlameTrails", 2, 0, random (0, 360), 2, random (60, 130))
		TNT1 AAA 8 BRIGHT A_CustomMissile ("MinimalFlameTrails", 2, 0, random (0, 360), 2, random (60, 130))
		Stop
    }
}

ACTOR FlamethrowerFireParticles
{
	Game Doom
	Radius 1
	Height 1
	Speed 2
	Damage 0 
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
    +CLIENTSIDEONLY
    +NOINTERACTION
	+NOCLIP
	+FORCEXYBILLBOARD
	DamageType Flames
	Renderstyle Add
    Scale 1.5
	Alpha 1.0
	Gravity 0
	States
	{
    Spawn:
	    TNT1 A 0
        FIR3 ABCD 2 BRIGHT A_SpawnItem("RedFlare", 0, 10)
		TNT1 A 0 A_CustomMissile ("BlackSmoke", 40, 0, random (0, 360), 2, random (40, 160))
		FIR3 EFGHIJKLMNOP 2 BRIGHT A_SpawnItem("RedFlare", 0, 10)
	    Stop
	}
}

ACTOR SmallFireTrail: FlameTrails
{
Scale 0.7
Speed 2
Alpha 0.7
Radius 1
Height 1
Renderstyle Add
	States
	{
    Spawn:
		TNT1 A 5
        TNT1 A 0 A_SpawnItem("RedFlare")
        FIR3 CDEFGHIJKLMNOP 2 BRIGHT
		Stop
	}
}

ACTOR FlamethrowerMissile
{
	Game Doom
	Radius 2
	Height 2
	Speed 25
	Damage 1 
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
	-RIPPER
	+NOBLOOD
	+NOBLOODDECALS
	+BLOODLESSIMPACT
	-BLOODSPLATTER
	RenderStyle Add
    DamageType Fire
    Scale 0.5
	Alpha 1.0
	Gravity 0
	States
	{
    Spawn:
	    TNT1 A 0 
        FIR3 A 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (0, 10))
		FIR3 B 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (0, 20))
		TNT1 A 0 A_Explode(3, 100, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (0, 30))
		FIR3 D 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (0, 40))
		FIR3 E 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (0, 50))
		FIR3 F 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_Explode(3, 100, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (-10, 60))
		FIR3 G 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (-50, 70))
		FIR3 H 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_CustomMissile ("SmallFireTrail", 6, 0, 0, 2, random (-60, 80))
		FIR3 I 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 0)
		TNT1 A 0 A_Explode(3, 100, 0)
		TNT1 A 0 A_CustomMissile ("FlamethrowerFireParticles", 6, 0, 0, 2, random (10, 20))
		FIR3 JKL 1 BRIGHT A_SpawnItem("RedFlare", 0, 0)
		TNT1 A 0 A_Explode(3, 150, 0)
		TNT1 A 0 A_CustomMissile ("FlamethrowerFireParticles", 6, 0, random (0, 360), 2, random (10, 90))
		Stop
	Death:
		TNT1 A 0 A_Explode(35, 150, 0)
		TNT1 A 0 A_Explode(3, 50)
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece2", random (-25, 25), random (-15, 15))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece", random (-15, 15), random (-15, 15))
		TNT1 AA 0 A_CustomMissile ("FlamethrowerFireParticles", 6, 0, random (0, 360), 2, random (10, 90))
		Stop
	}
}

ACTOR TinyBurningPiece: SmokeColumn
{
	Alpha 0.9
	RenderStyle Add
	Scale 1.1
	States
	{
	Spawn:
	
	CFCF ABC 1 BRIGHT
	CFCF D 1
	CFCF EFG 1 BRIGHT
	CFCF H 1
	CFCF IJK 1 BRIGHT
	CFCF L 1
	TNT1 A 0 A_Jump(24, "StopBurning")
	Loop
	
	StopBurning:
	CFCF NOP 1 BRIGHT A_SpawnItem("RedFlareSmall", 0, 5)
	Stop
    }
}

ACTOR TinyBurningPiece2: TinyBurningPiece
{
	Scale 0.7
	+CLIENTSIDEONLY
}

ACTOR TinyBurningPiece3: TinyBurningPiece2
{
	Scale 0.5
	+CLIENTSIDEONLY
}

ACTOR PentagramParticleSpawner
{
	Game Doom
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
	-BLOODSPLATTER
	-RANDOMIZE
	RenderStyle Add
	+NOCLIP
	+NOINTERACTION
	radius 40
	height 1
	gravity 1
	alpha 1
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 {
			A_SpawnItemEx("PentagramParticle", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 1);
			A_SpawnItemEx("PentagramParticle", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 1);
			A_SpawnItemEx("PentagramParticle", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 2);
			A_SpawnItemEx("PentagramParticle", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 3);
			A_SpawnItemEx("CoolAndNewFlameTrailsLong", random(0 - radius, radius), random(0 - radius, radius), random(0, 20), 0, 0, 0);
		}
		Stop
	}
}

ACTOR PentagramParticleSpawnerGreen : PentagramParticleSpawner replaces TeleportFog
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("world/teleport")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 {
			A_SpawnItemEx("PentagramParticleGreen", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 1);
			A_SpawnItemEx("PentagramParticleGreen", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 1);
			A_SpawnItemEx("PentagramParticleGreen", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 2);
			A_SpawnItemEx("PentagramParticleGreen", random(0 - radius, radius), random(0 - radius, radius), 0, 0, 0, 3);
			A_SpawnItemEx("GreenFlameTrailsTeleport", random(-24, 24), random(-24, 24), random(0, 60), 0, 0, 0);
		}
		Stop
	}
}

ACTOR PentagramParticle
{
	var int user_rotationspeed;

	+NOCLIP
	+NOINTERACTION
	+NOGRAVITY
	+ROLLSPRITE
	+ROLLCENTER
	-RANDOMIZE
	+FORCEXYBILLBOARD
	Scale 0.06
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(128, "SkipScaling")
		TNT1 A 0 A_SetScale(0.2)
	SkipScaling:
		TNT1 A 0 A_SetUserVar("user_rotationspeed", random(1, 5))
		TNT1 A 0 A_SetUserVar("user_rotationspeed", user_rotationspeed * ((random(0,1)*2)-1)) //Randomly -1 or 1!
		TNT1 A 0 A_Jump(256, "SpawnLoop")
	Boost:
		TNT1 A 0 ThrustThingZ(0, 1, 0, 1)
		TNT1 A 0 A_Jump(256, "SpawnLoop") //Needed for subclass
	SpawnLoop:
		TNT1 A 0 A_SetRoll(roll + user_rotationspeed)
		PNTG C random(1,2) BRIGHT A_FadeOut(0.03)
		PNTG C random(2,3) BRIGHT A_FadeOut(0.03)
		TNT1 A 0 A_Jump(256, "SpawnLoop", "SpawnLoop", "Boost")
	}
}

ACTOR PentagramParticleGreen : PentagramParticle
{
	States
	{
	SpawnLoop:
		TNT1 A 0 A_SetRoll(roll + user_rotationspeed)
		PNTG D random(1,2) BRIGHT A_FadeOut(0.03)
		PNTG D random(2,3) BRIGHT A_FadeOut(0.03)
		TNT1 A 0 A_Jump(256, "SpawnLoop", "SpawnLoop", "Boost")
	}
}