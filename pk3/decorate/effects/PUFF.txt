/** Hit puff effect that replaces the standard Doom one with some ricochet and fanciness. This one is used directly by bullet wepapons */
ACTOR HitPuff Replaces BulletPuff
{
    renderstyle Translucent
    alpha 0.4
    Scale 1.5
    radius 0
    height 0
    +NOBLOCKMAP
    +BLOODSPLATTER
    Gravity 0.01
    +NOEXTREMEDEATH
    +FORCEXYBILLBOARD
    DamageType Bullet
    Decal "BulletDecalNew1"
    +DONTSPLASH
    -EXPLODEONWATER
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx ("Ricochet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
    Melee:
        TNT1 A 1
        Stop
    }
}

/** Puff that does Shotgun damage type */
actor ShotgunPuff : HitPuff
{
    DamageType Shotgun
}

/** Puff that does RapidFire damage type */
Actor MachineGunBulletPuff: HitPuff
{
    DamageType RapidFire
}

/** Puff that does HitPuff damage type */
actor FlakPuff : HitPuff
{
    DamageType Flak
}

/** Given to WallChunk with a random amount when it's created. Is not actually a timer of any description whatsoever. */
Actor Timer : Inventory
{
inventory.maxamount 4
}

/** This is used as a flag out of the ACS CheckActorOverInventory - if given, it will cause a floor crater to kick up mud/dirt */
Actor IsOverGrass : Inventory
{
inventory.maxamount 1
}

/** Ricochet effects for when bullets hit a surface or liquid. */
ACTOR Ricochet
{
  renderstyle Add
  alpha 1.0
  -COUNTKILL
  -SHOOTABLE
  +CLIENTSIDEONLY
  +BLOODLESSIMPACT 
  +FORCEXYBILLBOARD
  +THRUACTORS
  +BLOODSPLATTER
  +NOTELEPORT
  Obituary "%o ate bullet shrapnel."
  Scale 0.09
  Speed 0
  Radius 1
  Height 1
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_CheckFloor("SpawnFloor")
    TNT1 A 0 A_SpawnItem ("YellowFlareSmall", 0)
    TNT1 A 0 A_SpawnItemEx ("DetectFloorBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 0 A_SpawnItemEx ("DetectCeilBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 AAAAAAAAAA 0 A_CustomMissile ("SparkX", 2, 0, random (0, 360), 2, random (0, 360))
    TNT1 A 0 bright A_PlaySound("impacts/ricochet")
    TNT1 AAA 0 A_CustomMissile ("SmokeTrail", 2, 0, random (0, 360), 2, random (0, 360))

    TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Spawn4")
    
    Spawn1:
    IPF2 AABB 1 bright A_SpawnItem ("YellowFlareSmall", 0)
    Stop
    
    Spawn2:
    IPF2 CCDD 1 bright A_SpawnItem ("YellowFlareSmall", 0)
    Stop
    
    Spawn3:
    IPF2 EEFF 1 bright A_SpawnItem ("YellowFlareSmall", 0)
    Stop
    
    Spawn4:
    IPF2 GGHH 1 bright A_SpawnItem ("YellowFlareSmall", 0)
    Stop

    SpawnFloor:
    TNT1 A 0
    TNT1 A 2
    TNT1 A 0 A_SpawnItemEx ("DetectFloorBullet",0,0,5,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 0 A_SpawnItemEx ("DetectCeilBullet",0,0,0,-5,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 AAAAAAAAAA 0 A_CustomMissile ("SparkX", 2, 0, random (0, 360), 2, random (30, 160))
    TNT1 A 0 bright A_PlaySound("impacts/ricochet")
    IPF2 GGHH 1 bright A_SpawnItem ("YellowFlareSmall", 0)
    TNT1 AAA 0 A_CustomMissile ("SmokeTrail", 2, 0, random (0, 360), 2, random (30, 160))
    stop
    
    SpawnWater:
    TNT1 A 0
    TNT1 A 0 A_SpawnItem("WaterRicochet")
    Stop
    
    SpawnSlime:
    TNT1 A 0
    TNT1 AAAAA 0 A_CustomMissile ("nukageParticleXFAster", 8, 0, random (0, 360), 2, random (30, 160))
    TNT1 AAAAA 0 A_CustomMissile ("nukageParticleXFAster", 24, 0, random (0, 360), 2, random (30, 160))
    TNT1 A 0 A_SpawnItem("SlimeRicochet")
    Stop
    
  }
}

ACTOR BigRicochet
{
  renderstyle Add
  alpha 1.0
  +CLIENTSIDEONLY
  +BLOODLESSIMPACT 
  +FORCEXYBILLBOARD
  +THRUACTORS
  +BLOODSPLATTER
  +MISSILE
  +NOTELEPORT
    Obituary "%o ate bullet shrapnel."
  damagetype "CauseWaterSplash"
  Scale 0.09
  Speed 0
  Radius 4
  Height 8
  states
  {
  Spawn:
    TNT1 A 0
    Goto Death
    
  Death:
    TNT1 A 2
    TNT1 A 1 A_Explode(4, 32)
    TNT1 A 0 A_CheckFloor("SpawnFloor")
    TNT1 A 0
    Stop
    
    SpawnFloor:
    TNT1 A 0
    TNT1 A 2
    TNT1 A 0
    stop
    
    SpawnWater:
    TNT1 A 0
    Stop
    
    SpawnSlime:
    TNT1 A 0
    TNT1 AAAAA 0 A_CustomMissile ("nukageParticleXFAster", 8, 0, random (0, 360), 2, random (30, 160))
    TNT1 AAAAA 0 A_CustomMissile ("nukageParticleXFAster", 24, 0, random (0, 360), 2, random (30, 160))
    TNT1 A 0 A_SpawnItem("SlimeRicochet")
    Stop
  }
}

ACTOR WaterRicochet: Ricochet
{
  renderstyle Translucent
  alpha 0.8
  Scale 1.0
  states
  {
  Spawn:
    SPHW ABCDE 3
    Stop
  }
}

ACTOR GiantWaterRicochet: Ricochet
{
  renderstyle Translucent
  alpha 0.9
  XScale 3.0
  YScale 2.0
  states
  {
  Spawn:
    GSPL ABCDEFGHIJKLMNOPQRSTUVWXYZ 2
    Stop
  }
}

ACTOR SlimeRicochet: Ricochet
{
  renderstyle Translucent
  alpha 0.8
  Scale 1.0
  states
  {
  Spawn:
    SPHG ABCDE 3
    Stop
  }
}

/** The chainsaw puff is a bit different, and includes code to produce a horrific screeching noise if it hits a wall. */
ACTOR SSawPuff: HitPuff
{
  renderstyle Translucent
scale 0.15
alpha 0.7
damagetype Saw
  +NOBLOCKMAP
  +BLOODSPLATTER
  +NOEXTREMEDEATH
  +FORCEXYBILLBOARD
  damagetype Cut
  states
  {
Spawn:
Death:
XDeath:
Melee:
        TNT1 A 0
        TNT1 AB 1 bright
        TNT1 A 0 A_Jump (128,3)
        TNT1 A 0 A_SpawnItem ("Sparks", 0)
        TNT1 AAAAAA 0 A_CustomMissile ("SparkX", 2, 0, random (0, 360), 2, random (30, 170))
        BPUF C 1 bright A_PlaySound("weapons/chainsaw/hitwall")
        BPUF D 1 bright
Stop    
  }
}

ACTOR Sparks: BulletPuff
{
  renderstyle Translucent
  alpha 0.8
  +NOBLOCKMAP
  +BLOODSPLATTER
  +CLIENTSIDEONLY
  Scale 0.5
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_SpawnItem ("YellowFlareSmall", 0)
    TNT1 A 0 A_Jump (132, 15)
    FX58 ABCDEFGHIJ 1 bright
    Stop
    TNT1 AAAAA 0
    FX57 ABCDEFGHIJ 1 bright
    stop
  }
}
