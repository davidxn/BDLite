ACTOR Doomer : PlayerPawn Replaces DoomPlayer
{
    Mass 100
    player.viewheight 44
    player.attackzoffset 16
    GibHealth 20
    PainChance 255
    Species "Marines"
    BloodType "BdBleedPlayer"
    DamageFactor "CauseObjectsToSplash", 0.0
    Player.ColorRange 112, 127
    Player.StartItem "BdRifle"
    Player.StartItem "BdMeleeAttacks"
    Player.StartItem "BdGrenades"
    Player.StartItem "RifleAmmo", 60
    Player.StartItem "RifleAmmoLoaded", 31
    Player.StartItem "ShotgunAmmoLoaded", 8
    Player.StartItem "PlasmaAmmoLoaded", 50
    Player.StartItem "RocketAmmoLoaded", 6
    Player.StartItem "GrenadeAmmo", 1
    Player.JumpZ 7.4
    Player.DisplayName "Modern"
    Player.CrouchSprite "PLYC"
    
    +SOLID
    +THRUSPECIES
    +MTHRUSPECIES
    +DONTHURTSPECIES
    +THRUGHOST

    States
    {
    Spawn:
        TNT1 A 0 NoDelay SetPlayerProperty(0, 1, PROP_FROZEN)
        TNT1 A 0 A_Changeflag("NOPAIN", 1)
        MARN D 1
        MARN D 1 ACS_NamedExecute("ScreenPainClear", 0, 0, 0, 0)
        TNT1 A 0
        {
            SetPlayerProperty(0, 0, PROP_FROZEN);
            A_Changeflag("NOPAIN", 0);
            if (GameType() == 2) // GameType 2 is GAME_NET_DEATHMATCH
            {
                A_ChangeFlag("THRUSPECIES", 0);
                A_ChangeFlag("MTHRUSPECIES", 0);
                A_ChangeFlag("DONTHURTSPECIES", 0);
            }
        }
    StandStill:
        MARN DDDEEEDDDEEEDDDEEE 5 A_JumpIf(velz != 0, "IsJumpin")
        TNT1 A 0
        {
            if (health < 25)
            {
                A_GiveInventory("HealthBonus", 1);
            }
        }
        loop
    
    See:
        MARN ABCB 6 A_JumpIf(velz != 0, "IsJumpin")
        TNT1 A 0
        {
            if (health < 25)
            {
                A_GiveInventory("HealthBonus", 1);
            }
        }
        goto StandStill

    IsJumpin:
        MARN A 3
        TNT1 A 0 A_JumpIf(velz != 0, "InAir")
        goto StandStill
        
    InAir:
        MARJ A 1
        TNT1 A 0 A_JumpIf(velz == 0, "Land")
        loop

    Land:
        TNT1 A 0 A_SetPitch(3.0 + pitch)
        MARN AAA 1 A_SetPitch(-1.0 + pitch)
        goto StandStill

    Kick:
        TNT1 A 0 A_TakeInventory("KickHasHit", 1)
        TNT1 A 0 A_JumpIf(velz != 0, "AirKick")
        TNT1 A 0 A_TakeInventory("KickHasHit", 1)
        PKIK AB 3
        PKIK CB 3 A_JumpIfInventory("KickHasHit", 1, "KickHit")
        goto StandStill

    AirKick:
        AKIK AAAAA 2 A_JumpIfInventory("KickHasHit", 1, "KickHit")
        goto StandStill

    KickHit:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_TakeInventory("KickHasHit", 1)
        TNT1 A 0 A_Recoil(3)
        goto StandStill

    Punch:
        PLA1 H 1
        PLA1 H 1 A_TakeInventory("Punching", 1)
        PLA1 E 4
        goto StandStill

    FistsStand:
        PLA1 BBBB 5
        {
            A_JumpIf(velx != 0 || vely != 0, "FistsMove");
            A_JumpIfInventory("Punching", 1, "Punch");
        }
        goto StandStill

    FistsMove:
        PLA1 ABCD 5 A_JumpIfInventory("Punching", 1, "Punch")
        TNT1 A 0 A_JumpIfInventory("Punching", 1, "Punch")
        TNT1 A 0 A_JumpIf(velx == 0 && vely == 0, "Spawn")
        goto See

    Pain:
        TNT1 A 0 A_Pain
        TNT1 A 0 ACS_NamedExecute("ScreenPainBloodSpots", 0, 0, 0, 0)
        PLAY G 5
        goto See
    Pain.Blast:
    Pain.Explosive:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBreak", 0, 0, 0, 0)
        Goto Pain
    Pain.Bullet:
    Pain.Flak:
    Pain.RapidFire:
        TNT1 A 0 A_Jump(256, "PBUL1", "PBUL2", "PBUL3", "PBUL4", "PBUL5", "PBUL6")
    PBUL1:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet1", 0, 0, 0, 0)
        Goto Pain
        PBUL2:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet2", 0, 0, 0, 0)
        Goto Pain
        PBUL3:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet3", 0, 0, 0, 0)
        Goto Pain
        PBUL4:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet4", 0, 0, 0, 0)
        Goto Pain
        PBUL5:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet5", 0, 0, 0, 0)
        Goto Pain
        PBUL6:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBullet6", 0, 0, 0, 0)
        Goto Pain
    Pain.Rip:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBloodSpots", 0, 0, 0, 0)
        TNT1 A 0 ACS_NamedExecute("ScreenPainScratch", 0, 0, 0, 0)
        Goto Pain
    Pain.Eat:
        TNT1 A 0 ACS_NamedExecute("ScreenPainBloodSpots", 0, 0, 0, 0)
        TNT1 A 0 ACS_NamedExecute("ScreenPainBite", 0, 0, 0, 0)
        Goto Pain
    Death:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_Jump(96, "Death.Arm")
        TNT1 A 0 A_CustomMissile("BdBloodSpawnerSmall", 50, 0, random(0, 360), 2, random(0, 160))
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        PLAY N -1
        Stop
    Death.Cut:
    Death.Saw:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL2 A 10 A_PlayerScream
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL2 B 20 A_NoBlocking
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL2 C 10 A_CustomMissile ("BdBloodSpawnerSmall", 40, 0, random (0, 360), 2, random (0, 160))
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerSmall", 30, 0, random (0, 360), 2, random (0, 160))
        XPL2 DE 10 A_CustomMissile ("BdBloodSpawnerSmall", 20, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        XPL2 E -1
        Stop
    Death.Slime:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_PlayerScream
        PMET AABBCCDDEEFFGGHHIIIII 10 A_CustomMissile ("MiniSmoke", 25, 0, random (0, 180), 2, random (0, 180))
        PMET IIIIIIIIIIJJJJJJJJJJJJJJJJJJJJJJKKKKKKKKKKKKKKKKKKKKKKKKKKLLLLLLLLLLLLLLL 3 A_CustomMissile ("MiniSmoke", 25, 0, random (0, 180), 2, random (0, 180))
        TNT1 A 0 A_NoBlocking
        TNT1 A -1
        Stop
    Death.Rip:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL3 A 10 A_XScream
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL3 B 20 A_NoBlocking
        XPL3 C 10 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL3 D 10 A_CustomMissile ("BdBloodSpawnerSmall", 40, 0, random (0, 360), 2, random (0, 160))
        XPL3 E 10 A_CustomMissile ("BdBloodSpawnerSmall", 30, 0, random (0, 360), 2, random (0, 160))
        XPL3 F 10 A_CustomMissile ("BdBloodSpawnerSmall", 20, 0, random (0, 360), 2, random (0, 160))
        XPL3 F 10 A_CustomMissile ("BdBloodSpawnerSmall", 0, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        XPL3 F -1
        Stop
    Death.Explosive:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        POSS Q 2 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile ("BdFlyingBlood", 32, 0, random (170, 190), 2, random (0, 40))
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerLarge", 22, 0, random (170, 190), 2, random (0, 15))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 32, 0, random (170, 190), 2, random (0, 40))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLumpBone", 32, 0, random (170, 190), 2, random (0, 40))
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        XDMG E -1
        Stop
    Death.SuperKick:
    Death.Blast:
    Death.Flak:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        POSS Q 2 A_FaceTarget
        TNT1 AAAA 0 A_CustomMissile ("BdFlyingBlood", 32, 0, random (170, 190), 2, random (0, 40))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodSpawnerLarge", 22, 0, random (170, 190), 2, random (0, 15))
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        POSS RSTUVW 8
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        POSS X -1
        Stop
    Death.RapidFire:
        TNT1 A 0 A_Jump(96, "Death.Arm")
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL4 A 20 A_XScream
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 50, 0, random (0, 360), 2, random (0, 160))
        XPL4 B 20 A_NoBlocking
        XPL4 CDEF 20 A_CustomMissile("BdBloodSpawnerSmall", 50, 0, random(0, 360), 2, random(0, 160))
        TNT1 A 0 A_CustomMissile("BdBloodSpawnerSmall", 50, 0, random(0, 360), 2, random(0, 160))
        TNT1 A 0 A_SpawnItem("BdBloodSpot")
        XPL4 G -1
        Stop
    Death.Arm:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 AAA 0 A_CustomMissile("BdBloodSpawnerSmall", 50, 0, random(0, 360), 2, random(0, 160))
        XPL5 A 10 A_PlayerScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("player/scream")
        XPL5 BCBCBCBC 10 A_CustomMissile ("BdBloodSpawnerSmall", 25, 0, random (0, 360), 2, random (0, 160))
        XPL5 DE 7 A_CustomMissile ("BdBloodSpawnerSmall", 15, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        XPL5 E -1
        Stop
    Death.SuperKick:
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 ThrustThingZ(0,30,0,1)
        TNT1 AAAAAAAAAAA 0 A_CustomMissile("BdBloodLump", 50, 0, random(0, 360), 2, random(0, 160))
        XPL6 A 5 A_XScream
        TNT1 A 0 A_NoBlocking
        XPL6 BCDE 5
        XPL6 F -1
        Stop
    Death.GreenFire:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 AAAA 0 A_CustomMissile("BdBleed", 30, 0, random(0, 360), 2, random(0, 160))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("GreenFlameTrails", 50, 0, random(0, 360), 2, random(0, 360))
        TNT1 AAAA 2 BRIGHT A_SpawnItem("GreenFlare", 0, 43)
        TNT1 A -1
        Stop    
    Death.Plasma:
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        PBR1 A 6 A_PlayerScream
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BdAshes", 32, 0, random(0, 360), 2, random(0, 180))
        PBR1 B 6 A_NoBlocking
        PBR1 CDEFGH 6 A_CustomMissile ("Blood", 10, 0, random (0, 360), 2, random (0, 160))
        PBR1 HHHHHHHHHHHHHHHHHHHHHH 8 A_CustomMissile ("MiniSmoke", 10, 0, random (0, 360), 2, random (0, 160))
        PBR1 H -1
        Stop
    Death.Burn:
        PBUR A 1 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        PBUR A 1 A_PlayerScream
        PBUR A 1 A_NoBlocking
        PBUR AAAABBBBCCCC 2 A_CustomMissile ("SmallFlameTrails", 32, 0, random (0, 180), 2, random (0, 180))
        PBUR DDDDEEEE 2 A_CustomMissile ("SmallFlameTrails", 16, 0, random (0, 180), 2, random (0, 180))
        PBUR EEEEE 4 A_CustomMissile ("SmallFlameTrails", 8, 0, random (0, 180), 2, random (0, 180))
        PBUR EEEEEEE 6 A_CustomMissile ("MiniSmoke", 8, 0, random (0, 180), 2, random (0, 180))
        PBUR E -1
        Stop
    Death.Fire:
    Death.Flames:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItem("PlayerBurning")
        TNT1 A 1
        TNT1 A -1
        Stop
    Crush:
        TNT1 A 0 ACS_NamedExecute("ScreenPainDead", 0, 0, 0, 0)
        TNT1 A 0 A_PlaySound("misc/xdeath4")
        CRS1 A -1
        Stop
    }
}

actor BdBleedPlayer : BdBleed
{
    states
    {
    Spawn:
        TNT1 A 0 NoDelay A_CustomMissile("BdFlyingBlood", 7, 0, random(0, 360), 2, random(0, 160))
        stop
    }
}

actor PlayerBurning
{
    Radius 1
    Height 1
    Mass 1000
    Health 100

    +NOTELEPORT
    +MOVEWITHSECTOR
    +NOICEDEATH
    States
    {
    Spawn:
       BUR2 A 1 NoDelay BRIGHT A_PlaySound("player/scream")
       BUR2 AABBCCDDEE 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 36, 0, random(0, 180), 2, random(0, 180))
       BURN FFGGHH 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 36, 0, random(0, 180), 2, random(0, 180))
       BURN IIJJKKLL 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 28, 0, random(0, 180), 2, random(0, 180))
       BURN MMN 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 32, 0, random(0, 180), 2, random(0, 180))
       BURN NOOPP 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 22, 0, random(0, 180), 2, random(0, 180))
       BURN QQ 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 18, 0, random(0, 180), 2, random(0, 180))
       TNT1 A 0 A_PlaySound("player/scream")
       BURN RRSSTTSSRRSSTTSSRRSSTTRRSSTTSSRRSSTTSSRRSSTTSSTT 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 12, 0, random(0, 180), 2, random(0, 180))
       BURN UU 3 BRIGHT A_CustomMissile("CoolAndNewFlameTrailsLong", 12, 0, random(0, 180), 2, random(0, 180))
       BURN VVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVVV 9 A_CustomMissile("MiniSmoke", 8, 0, random(0, 360), 2, random(90, 110))
       BURN V -1
       stop
    }
}

actor PlayerQuakeSmall
{
    Height 8
    Radius 4

    +NOBLOCKMAP
    +BLOODSPLATTER
    +NOSECTOR
    +NOCLIP
    +CLIENTSIDEONLY

    States
    {
    Spawn:
        TNT1 A 10 NoDelay A_Quake(1, 4, 0, 64)
        stop
    }
}

actor PlayerQuakeLarge : PlayerQuakeSmall
{
    States
    {
    Spawn:
        TNT1 A 10 NoDelay A_Quake(8, 6, 0, 64)
        stop
    }
}