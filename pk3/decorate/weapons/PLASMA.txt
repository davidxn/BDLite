ACTOR BdPlasmaGun : BdLiteWeapon Replaces PlasmaRifle
{
    Weapon.SelectionOrder 100
    Weapon.AmmoGive2 50
    Weapon.SlotNumber 6
    Weapon.AmmoType1 "PlasmaAmmoLoaded"
    Weapon.AmmoType2 "PlasmaAmmo"
    Inventory.PickupSound "weapons/plasma/draw"
    Inventory.PickupMessage "$GOTPLASMA"
    States
    {
    Spawn:
        PLAS A -1
        Stop
    Ready:
        TNT1 A 0 A_PlaySound("weapons/plasma/draw")
        PLS9 ABC 1
    ReadyLoop:
        PLSG A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
        Goto ReadyLoop
    Deselect:
        PLS9 CBA 1
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1
        Wait

    Fire:
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded", 1, 1)
        Goto Reload

        TNT1 A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("PlasmaFlareSpawn", 0, 0, 0, 0)
        PLSF A 1 BRIGHT A_FireCustomMissile("PlasmaBall", 0, 1, 0, 0)
        TNT1 A 0 A_Takeinventory("PlasmaAmmoLoaded", 1)
        PLSA B 1 BRIGHT A_FireCustomMissile("PlayerQuakeSmall", 0, 0, 0, 0)
        PLSA C 1 BRIGHT
        PLSF B 1 BRIGHT
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_PlaySound("weapons/plasma/cool", 0)
        PLSG BCD 1
        PLSG EEEEEE 3 {
            A_FireCustomMissile("SmokeTrail",0,0,10,2);
            A_WeaponReady(WRF_NOPRIMARY | WRF_ALLOWZOOM);
        }
        PLSG DCB 1
        TNT1 A 0 A_PlaySound("weapons/plasma/beepbeep")
        Goto ReadyLoop
    Reload:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded", 0, "ReadyLoop")
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmo", 1, "ReloadAnimate")
        //No more ammo to reload
        TNT1 A 0 A_PlaySound("weapons/plasma/beep")
        PLSR A 4 A_WeaponReady(WRF_NOFIRE)
        TNT1 A 0 A_PlaySound("weapons/plasma/beep")
        PLSR A 4 A_WeaponReady(WRF_NOFIRE)
        TNT1 A 0 A_CheckReload
        Goto ReadyLoop
    ReloadAnimate:
        TNT1 A 0 A_PlaySound("weapons/plasma/ready")
        PLSR AB 1
        TNT1 A 0 A_SpawnItemEx("EmptyCell", 17, 2, 35, 0, random(3,6), 4)
        PLSR CDEEEEEEEEEEEEEEEF 1
        PLSR GHHIIIJJJJJJJJIKLBAAAAA 1
    ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded", 0, "ReadyLoop")
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmo",1,1)
        Goto ReadyLoop
        TNT1 A 0 A_Giveinventory("PlasmaAmmoLoaded",1)
        TNT1 A 0 A_Takeinventory("PlasmaAmmo",1)
        Goto ReloadAmmo
    }
}

/** Ammunition for plasma weapons */
Actor PlasmaAmmo : Ammo replaces Cell 
{
    //$Category Ammunition
    Inventory.PickupMessage "$GOTCELL"
    Inventory.Amount 20
    Inventory.MaxAmount 300
    Ammo.BackpackAmount 20
    Ammo.BackpackMaxAmount 600
    Inventory.Icon "CELLA0"
    States
    {
    Spawn:
        CELL ABCDE 4
        Loop
    }
}

/** Large ammunition for plasma weapons */
Actor PlasmaAmmoPack : PlasmaAmmo replaces CellPack
{
    Inventory.PickupMessage "$GOTCELLBOX"
    Inventory.Amount 100
    States
    {
    Spawn:
        CELP ABCDE 4
        Loop
    }
}

/** A single-cell pickup, used for monster drops where the normal cell drop amount would be far too many */
Actor PlasmaAmmoSingle : PlasmaAmmo
{
    Inventory.PickupMessage "Picked up a single cell"
    Inventory.Amount 1
    States
    {
    Spawn:
        CEL1 AB 6
        Loop
    }
}

/** Represents ammo loaded into the plasma gun */
ACTOR PlasmaAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 50
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 50
   Inventory.Icon "PLASA0"
}
