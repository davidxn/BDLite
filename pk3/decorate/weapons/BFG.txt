ACTOR BdBFG : BdLiteWeapon Replaces BFG9000
{
    Height 20
    Weapon.SlotNumber 7
    Weapon.SelectionOrder 2800
    Weapon.AmmoUse 40
    Weapon.AmmoGive 40
    Weapon.AmmoType "PlasmaAmmo"
    Inventory.PickupSound "weapons/bfg/draw"
    Inventory.PickupMessage "$GOTBFG9000"
    States
    {    
    Spawn:
        BFUG A -1
    Ready:
        TNT1 A 0 A_PlaySound("weapons/bfg/draw")
        BFGG A 1 Offset(-50, 80)
		BFGG A 1 Offset(-30, 65)
		BFGG A 1 Offset(-15, 50)
    ReadyLoop:
        BFGG A 4 A_WeaponReady(WRF_ALLOWZOOM)
        Goto ReadyLoop
    Deselect:
		BFGG A 1 Offset(-15, 50)
		BFGG A 1 Offset(-30, 65)
        BFGG A 1 Offset(-50, 80)
        TNT1 A 1 A_Lower
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
        Wait
    Fire:
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmo", 40, "FireOK")
        BFGG A 8 A_PlaySound("weapons/plasma/beepbeep")
        Goto ReadyLoop
    FireOK:
        TNT1 A 0 A_PlaySound("weapons/bfg/charge")
        TNT1 A 0 A_AlertMonsters
        BFGC ABABABABABABABAB 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        BFGF A 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        TNT1 A 0 A_PlaySound("weapons/bfg/fire")
        TNT1 AAA 0 A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        TNT1 A 0 A_Recoil(8)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("SuperBFGBall", 0, 0)
        TNT1 A 0 A_TakeInventory("PlasmaAmmo", 40)
        BFGF B 2 BRIGHT
        TNT1 A 0 A_FireCustomMissile("PlayerQuakeLarge", 0, 0, 0, 0)
        BFGF CDEF 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        BFGG A 1 BRIGHT
        BFGG A 4
        TNT1 A 0 A_Refire
        Goto ReadyLoop
    }
}
