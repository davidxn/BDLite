ACTOR BdRocketLauncher : BdLiteWeapon Replaces RocketLauncher
{
    SpawnID 29
    Weapon.SelectionOrder 9500
    Weapon.AmmoGive2 6
    Weapon.AmmoType1 "RocketAmmoLoaded"
    Weapon.AmmoType2 "RocketAmmo"
    Weapon.SlotNumber 5
    Inventory.PickupSound "weapons/rocket/pickup"
    Inventory.PickupMessage "$GOTLAUNCHER"

    States
    {
    Spawn:
        LAUN A -1
        Stop
    Ready:
        TNT1 A 2 A_PlaySound("weapons/rocket/draw")
        MISR EDCB 1
    ReadyLoop:
        MISG A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
        Goto ReadyLoop
    Deselect:
        MISR BCDE 1
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1
        Wait
    Fire:
        TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded",1,1)
        Goto Reload

        TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
        TNT1 A 0 A_PlaySound ("weapons/rocket/fire")
        TNT1 A 0 A_AlertMonsters
        MISF A 1 BRIGHT A_FireCustomMissile("BdRocket", 0, 1, 0, -10)
        TNT1 A 0 A_Takeinventory("RocketAmmoLoaded",1)
        MISF BCD 1 BRIGHT
        MISF E 1 A_SetPitch(-6.0 + pitch)
        MISG BCD 2 A_SetPitch(2.0 + pitch)
        MISG EF 2
        TNT1 A 0
        TNT1 A 0 A_Refire
        Goto ReadyLoop

    Reload:
        MISG A 1 A_WeaponReady
        MISG A 6
        TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded", 0,"ReadyLoop")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1, "ReloadAnimate")
        Goto ReadyLoop
    ReloadAnimate:
        TNT1 A 0 A_PlaySound("weapons/rocket/reload")
        TNT1 A 0 A_SetAngle(-2 + angle)
        TNT1 A 0 A_SetPitch(+2 + pitch)
        MISR ABCDDE 1 
        TNT1 A 0 A_SpawnItemEx("EmptyRocketChamber", 17, 2, 35, 0, random(3,6), 4)
        MISR EFFGG 1
        TNT1 A 0 A_SetAngle(+2 + angle)
        TNT1 A 0 A_SetPitch(-2 + pitch)
        MISR HHIIJJKLM 1
        TNT1 A 0 A_SetAngle(+1 + angle)
        TNT1 A 0 A_SetAngle(-1 + angle)
        MISR OOOPQ 1
        MISG DEF 2
        MISG A 6
    ReloadAmmo:
        //This continuously subtracts from stock and adds to the amount in the weapon until we either run out of stock or fill the weapon.
        TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded", 0,"ReadyLoop")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1, 1)
        Goto ReadyLoop
        TNT1 A 0 A_Giveinventory("RocketAmmoLoaded", 1)
        TNT1 A 0 A_Takeinventory("RocketAmmo", 1)
        Goto ReloadAmmo
    }
}

/** Represents ammo loaded into the BdRocketLauncher */
ACTOR RocketAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   Inventory.Icon "LAUNA0"
}
