ACTOR BdSpiderMastermind : BdLiteMonster Replaces SpiderMastermind
{
    var int user_loop_index;

    damagefactor "Explosive", 1.5
    Obituary "%o was minced by a Mastermind."
    +NOICEDEATH
    SpawnID 7
    Health 4000
    Radius 100
    damagefactor "Crush", 30.0
    Scale 1.5
    Height 150
    Mass 500000
    Speed 10
    PainChance 30
    PainChance "Explosive", 80
    +BOSS
    +DONTRIP
    +DONTMORPH
    +BOSSDEATH
    +NORADIUSDMG
    +NOBLOOD
    +MISSILEMORE
    +MISSILEEVENMORE
    +SHOOTABLE
    SeeSound "spider/sight"
    AttackSound "spider/attack"
    PainSound "spider/pain"
    DeathSound "spider/death"
    ActiveSound "spider/active"
    damagefactor "Blood", 0.0
    Species SpiderMastermind
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ceilingz-floorz >= height, "HeightOK")
        TNT1 A 0 A_SetScale(1)
        TNT1 A 0 A_SetSize(-1, 100)
    HeightOK:
        SPID A 10 A_Look
        Loop
    See:
        SPID A 2 A_Metal
        SPID A 2 A_Recoil(-3)
        SPID B 2 A_Chase
        SPID B 2 A_Recoil(-3)
        SPID C 2 A_Chase
        SPID C 2 A_Recoil(-3)
        TNT1 A 0 A_SpawnItemEx("MasterMindStep", 90, 90)
        TNT1 A 0 A_SpawnItemEx("MasterMindStep", -90, 90)
        SPID D 2 A_Recoil(-3)
        SPID D 2 A_Metal
        SPID E 2 A_Recoil(-3)
        SPID E 2 A_Recoil(-3)
        SPID F 2 A_Chase
        TNT1 A 0 A_SpawnItemEx("MasterMindStep", 90, -90)
        TNT1 A 0 A_SpawnItemEx("MasterMindStep", -90, -90)
        Loop

    Missile:
        TNT1 A 0 A_SetUserVar("user_loop_index", 0)
        SPID A 4 A_FaceTarget 
        SPID A 4 A_FaceTarget 
        SPID A 4 A_FaceTarget 
        SPID H 4 A_FaceTarget
    MissileLoop:
        TNT1 A 0 A_JumpIfCloser(200, "StopFiring")
        SPID G 2 A_FaceTarget
        SPID H 2 BRIGHT A_CustomMissile("MastermindTracer", 72, 0, random(-7, 7), 1, random(-4, 2))
        TNT1 A 0 A_SpawnItemEx("BigShotgunCasing", 17, 30, 70, 0, random(10,30), random(10,30))
        TNT1 A 0 A_SetUserVar("user_loop_index", user_loop_index+1)
        TNT1 A 0 A_JumpIf(user_loop_index > 40, "StopFiring")
        
        SPID G 0 A_SpidRefire
        Goto MissileLoop
    
    StopFiring:
        SPID A 6 A_FaceTarget 
        Goto See

    Pain:
        SPID I 4 A_Pain
        Goto See

    Death:
    XDeath:
    DieNormal:
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,0,72,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerLarge", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibMastermindJunk1", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdGibMastermindJunk2", 96, 0, random (0, 360), 2, random (0, 160))
        SPIY A 30 A_Pain
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,-16,10,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMelting", 80, 0, random (0, 360), 2, random (0, 160))
        SPIY A 10
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,40,16,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerLarge", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibMastermindJunk1", 96, 0, random (0, 360), 2, random (0, 160))
        SPIY A 20 A_Pain
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,72,60,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerMelting", 120, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibMastermindJunk2", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdGibMastermindJunk1", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLump", 60, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 60, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdGibGut", 60, 0, random (0, 360), 2, random (10, 160))
        SPIY A 10
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,-72,40,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_CustomMissile ("BdGibMastermindJunk1", 96, 0, random (0, 360), 2, random (0, 160))
        SPIY A 8
        TNT1 AAAAA 0 A_SpawnItemEx ("BdExplosionBig",random(-50,50),random(-50,50),20,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMelting", 90, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLump", 60, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 60, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibMastermindLeg", 40, 30, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibMastermindJunk2", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibMastermindJunk1", 96, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_Scream
        TNT1 A 0 A_BossDeath
        TNT1 A 0 A_NoBlocking
        Stop
    }
}
